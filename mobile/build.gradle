apply plugin: 'com.android.application'

def versionMajor = Integer.parseInt(APP_VERSION_MAJOR)
def versionMinor = Integer.parseInt(APP_VERSION_MINOR)
def versionPatch = Integer.parseInt(APP_VERSION_PATCH)
def versionBuild = Integer.parseInt(APP_VERSION_BUILD)

android {
    compileSdkVersion 21
    buildToolsVersion '20.0.0'

    lintOptions { // TODO Remove this
        checkReleaseBuilds false
    }

    defaultConfig {
        applicationId project.PACKAGE_NAME
        minSdkVersion Integer.parseInt(ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"
        manifestPlaceholders = [ watchfaceName:project.WATCHFACE_NAME, appName:project.APP_NAME ]

        applicationVariants.all { variant ->
            def watchfaceName = project.WATCHFACE_NAME.replace(" ", "").toLowerCase()
            def file = variant.outputFile
            def suffix = defaultConfig.versionName
            def outputFile = file.name.replace(".apk", "-" + suffix + ".apk")
            outputFile = outputFile.replace("mobile", watchfaceName)
            variant.outputFile = new File(file.parent, outputFile)
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile file("../release-keystore.jks")
            keyAlias 'testing'
            storePassword 'testing'
            keyPassword 'testing'
        }
    }

    buildTypes {
        release {
            runProguard false
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    wearApp project(':wear')
    compile 'com.jakewharton:butterknife:5.1.+'
    compile 'com.jakewharton.timber:timber:2.4.+'
    compile 'com.netflix.rxjava:rxjava-android:0.20.5'
    compile 'com.squareup.retrofit:retrofit:1.7.1'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.google.android.gms:play-services-wearable:+'
    compile 'com.google.guava:guava:18.0'
}

// uncomment this line to ask for release passwords at the command line
//apply from: 'https://raw.github.com/twotoasters/HelloAndroidStudio/master/gradle/keystore.gradle'
